#!perl
use v5.14;
use Getopt::Long;
use Win32::Console;
use Win32::Console::ANSI;
use Term::ANSIColor;
use Data::Dump qw{ dump };

my $highlight = 0;
GetOptions(
    'color'       => \my $colored,
    'match-only'  => \my $match_only,
    'highlight=i' => \$highlight
);

my $pattern = shift @ARGV;
my @files   = @ARGV ? @ARGV : ();

if ( @files ) {
    for my $f (@files) {
        next if ( $f eq '.' || $f eq '..' );

        # get multiple files in win32
        if ( $f =~ /\*/ ) {
            push @files, grep { ! -d $_ } glob($f);
            next;
        }

        if ( @files > 1 ) {
            say $colored ? colored("$f:", 'magenta on_white') : "$f:";
        }

        open my $fh, '<', $f or die "Can't open '$f'";
        match($_) for <$fh>;
        close $fh;
    }
} else {
    match($_) for <>;
}

sub match {
    if (my @matches = /($pattern)/i) {
        my $match = $colored ? colored($matches[0], 'bold red') : "**$matches[0]**";
        s/$pattern/$match/i;
        if ( $match_only ) {
            if ( $highlight == 0 ) {
                say join '', map { $_ . " " x (40 - length $_) }
                             map { $colored ? colored($_, 'bold red') : $_ }
                             @matches > 1 ? @matches[1..$#matches] : @matches;
            } else {
                if ( defined(my $m = $matches[$highlight]) ) {
                    say $colored ? colored($m, 'bold red') : $m;
                } else {
                    say "You only have ", $#matches, " matches ",
                        "try indexing the '-h' flag with ",
                        join(', ', (1..$#matches - 1)), " or ", $#matches;
                    exit(0);
                }
            }
        } else { print }
        return 1;
    } else { return 0 }
}

__END__

=head1 NAME

C<grep> - A simple cross-platform grep clone

=head1 USAGE

=head1 SYNOPSIS

=head1 AUTHOR

Delon Newman L<delon.newman@gmail.com>

=end
